Program.Sub.Preflight.Start
Program.Sub.Preflight.End

Program.Sub.Main.Start
'GB 5/21/19   Crystorama needs to lock their new orders with a 'hold' until all lines are entered.
' we will set the shp_hold_flg on the order_header screen shp_hld_flag(#000096) on the populate hook(11560)to '04' whenever the order_header is first created, 
' then we will give the user a message to see if they want to release the hold whenever the order header screen is exited or saved
'GCG_5529_HOLD_ORDER.g2u

f.Intrinsic.Control.Try
V.Local.sError.Declare(String, "")

    F.Intrinsic.Control.If(V.Caller.Switches,=,"N") 
		f.Intrinsic.Control.SelectCase(v.Caller.Hook)		
'populate hook on order header screen - we will turn on the shipping hold flag for all new orders
            f.Intrinsic.Control.Case(11560)
               v.Passed.000096.Set("Y")
'post save hook on order header screen
            f.Intrinsic.Control.Case(11872)         
               f.Intrinsic.Control.CallSub(RemoveHoldFlag)
'pre exit hook on order header screen               
            f.Intrinsic.Control.Case(11900)
'if the ship hold flag is on            
              f.Intrinsic.Control.If(v.Passed.000096,=,"Y")
                 f.Intrinsic.Control.CallSub(RemoveHoldFlag)
              f.Intrinsic.Control.EndIf
        f.Intrinsic.Control.EndSelect
	F.Intrinsic.Control.EndIf
f.Intrinsic.Control.Catch
    f.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
    f.Intrinsic.UI.Msgbox(V.Local.sError)
f.Intrinsic.Control.EndTry
Program.Sub.Main.End

Program.Sub.RemoveHoldFlag.Start
f.Intrinsic.Control.Try
V.Local.sError.Declare(String, "")

v.Local.sSql.Declare
v.Local.sOrderNo.Declare
v.Local.iLineCount.Declare
v.Local.iReturn.Declare

v.Local.sOrderNo.Set(v.Passed.000003)
'have to pad the order number as passed variable has the leading zeroes, if exist, cut out.
F.Intrinsic.String.Lpad(V.Local.sOrderNo,"0",7,V.Local.sOrderNo)
'open DB connection
f.ODBC.Connection!Con.OpenCompanyConnection
   f.Intrinsic.String.Build("SELECT COUNT(ORDER_NO) FROM ORDER_LINES WHERE ORDER_NO = '{0}' AND RECORD_TYPE = 'L'",V.Local.sOrderNo,V.Local.sSQL)
   F.ODBC.Connection!Con.ExecuteandReturn(v.Local.sSql,v.Local.iLineCount)
   F.Intrinsic.Control.If(V.Local.iLineCount,>,0)
      f.Intrinsic.UI.Msgbox("Would you like to release the order from shipping hold? ","Release Order",4,v.Local.iReturn)
      f.Intrinsic.Control.If(v.Local.iReturn,=,6)   
         F.Intrinsic.String.Build("UPDATE ORDER_HEADER SET SHP_HLD_FLAG = '00' WHERE ORDER_NO = '{0}' AND SHP_HLD_FLAG = '04' AND RECORD_TYPE = 'A'",V.Local.sOrderNo,V.Local.sSQL)
         F.ODBC.Connection!Con.Execute(V.Local.sSQL)
      f.Intrinsic.Control.EndIf
   F.Intrinsic.Control.EndIf
'close the DB connection
f.ODBC.Connection!Con.Close

f.Intrinsic.Control.Catch
    f.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
    f.Intrinsic.UI.Msgbox(V.Local.sError)
f.Intrinsic.Control.EndTry
Program.Sub.RemoveHoldFlag.End

Program.Sub.Comments.Start
${$0$}$$}$$}$12:00:00 AM$}$False
Program.Sub.Comments.End